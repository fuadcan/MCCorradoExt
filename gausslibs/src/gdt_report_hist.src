/*
** gdt_report_hist.src - Compute histogram
**
** (C) Copyright 2003 by Aptech Systems, Inc.
** All Rights Reserved.
**
** This Software Product is PROPRIETARY SOURCE CODE OF APTECH
** SYSTEMS, INC.    This File Header must accompany all files using
** any portion, in whole or in part, of this Source Code.   In
** addition, the right to create such files is strictly limited by
** Section 2.A. of the GAUSS Applications License Agreement
** accompanying this Software Product.
**
** If you wish to distribute any portion of the proprietary Source
** Code, in whole or in part, you must first obtain written
** permission from Aptech Systems.
**
** Purpose:
**
**      To compute histogram for variables contained in a
**      GAUSS data file.
**
** Format:
**
**      { r,n } = _gdt_report_hist(data, args, names, types);
**
** Inputs:
**
**      data      NxK matrix, data for frequency distributions.
**
**      args      Lx1 string array, keywords for freq options
**
**                     use   variable list
**                  numcat   number of categories, default = 10
**                 percent   percentage histogram
**               frequency   frequency histogram, default
**
**      names     Kx1 vector with names of variables in data for which
**                   frequency distributions are requested.
**
**      types     Kx1 vector, types of variables in data, 0 character,
**                     1 - numeric, 2 - date
**
** Outputs:
**
**     r     scalar, return code
**
**     s     scalar, return code
**
**
** Examples:
**
*/

#include pgraph.ext



proc (2) = _gdt_report_hist(data,args,names,types);
    local usevars,s1,s2,indx,numcat,htype,oldm;

    useVars = error(0);
    numcat = 10;
    htype = 2;

    for i(1,rows(args),1);

        if args[i] $/= "";

            { s1,s2 } = token(args[i]);
            if lower(s1) $== "use";
                 useVars = strsplit(s2)';
            elseif lower(s1) $== "numcat";
                 numcat = stof(s2);
            elseif lower(strsect(s1,1,3)) $== "per";
                htype = 1;
            elseif lower(strsect(s1,1,4)) $== "freq";
                htype = 2;
            endif;
        endif;

     endfor;

    if not scalmiss(useVars);
        indx = indsav(useVars,names);
    else;
        indx = seqa(1,1,rows(names));
    endif;

    if rows(indx) < 2;
       errorlog "two or more variables are required for scattergrams";
       retp(0,-1);
    endif;

    graphset;
    _paxht = .25;
    oldm = setvwrmode("many");
    for i(1,rows(indx),1);
         call xlabel(names[indx[i]]);
         if htype == 1;
             call histp(data[.,indx[i]],numcat);
         else;
             call hist(data[.,indx[i]],numcat);
         endif;
/*         vwr graphic.tkf -wp=10,10,640,480 */
    endfor;
    call setvwrmode(oldm);
    retp(-1,0);

endp;



proc(0) = _gdt_report_hist_help;

       local s1;

       s1 = "
Purpose: generates histogram plots

Format:  report hist

Example: report hist
         use age,pay
         percent

Options: use       list of variables, default is all variables
         numcat    number of categories, default = 10
         percent   percentage histogram
         frequency frequency histogram, default";

       print s1;

endp;

