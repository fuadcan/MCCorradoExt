/*
** unionsa.src
** (C) Copyright 1988-2003 by Aptech Systems, Inc.
** All Rights Reserved.
**
** This Software Product is PROPRIETARY SOURCE CODE OF APTECH
** SYSTEMS, INC.    This File Header must accompany all files using
** any portion, in whole or in part, of this Source Code.   In
** addition, the right to create such files is strictly limited by
** Section 2.A. of the GAUSS Applications License Agreement
** accompanying this Software Product.
**
** if you wish to distribute any portion of the proprietary Source
** Code, in whole or in part, you must first obtain written
** permission from Aptech Systems.
**
**> unionsa
**
**  Purpose:    Returns the union of two string vectors with duplicates removed. 
**
**  Format:     y = unionsa(sv1,sv2);
**
**  Input:      sv1     Nx1 or 1xN string vector.
**
**              sv2     Mx1 or 1xM string vector.
**
**  Output:     y       Lx1 vector containing all unique values that are in sv1
**                      and sv2, sorted in ascending order. 
**
**  Globals:    None
**
**  Example:    sv1 = { "mary", "jane", "linda", "john" };
**              sv2 = { "mary", "sally" };
**              y = unionsa(sv1,sv2);
**
**              y =   jane
**                    john
**                   linda
**                    mary
**                   sally
*/

proc (1) = unionsa(sv1,sv2);
    local i,sv,nrows;

    if rows(sv1) == 1;
        if cols(sv1) > 1;
            sv1 = sv1';
        endif;
    endif;

    if rows(sv2) == 1;
        if cols(sv2) > 1;
            sv2 = sv2';
        endif;
    endif;

    sv = {};
    nrows = rows(sv1);

    // first vector
    for i (1, nrows, 1);
        if (sv1[i] $/= sv);
            sv = sv $| sv1[i];
        endif;
    endfor;

    // second vector
    nrows = rows(sv2);
    for i (1, nrows, 1);
        if (sv2[i] $/= sv);
            sv = sv $| sv2[i];
        endif;
    endfor;

    retp(sortcc(sv,1));
endp;
